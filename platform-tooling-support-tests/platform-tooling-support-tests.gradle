apply from: "$rootDir/gradle/testing.gradle"

// Use Java 10
project.ext.javaVersion = JavaVersion.VERSION_1_10
afterEvaluate {
	tasks.withType(JavaCompile) {
		sourceCompatibility = project.ext.javaVersion
		targetCompatibility = project.ext.javaVersion
		options.encoding = 'UTF-8'
		options.compilerArgs += '-parameters'
		options.compilerArgs += ['--release', project.ext.javaVersion.majorVersion]
		// https://discuss.gradle.org/t/getting-more-information-on-gradle-compilejava-annotation-processor-warning/28076
		options.annotationProcessorPath = configurations.annotationProcessor
	}
}

dependencies {
	implementation("de.sormuras:bartholdy:${Versions.bartholdy}") {
		because 'manage external tool installations'
	}
	implementation("commons-io:commons-io:${Versions.commonsIo}") {
		because 'moving/deleting directory trees'
	}

	testImplementation("com.paypal.digraph:digraph-parser:${Versions.digraphParser}") {
		because 'parsing .dot files (generated by jdeps)'
	}
	testImplementation("org.jgrapht:jgrapht-core:${Versions.jgrapht}") {
		because 'building and analyzing graphs'
	}
	testImplementation("com.tngtech.archunit:archunit-junit5-api:${Versions.archunit}") {
		because 'checking the architecture of JUnit 5'
	}
	testRuntimeOnly("com.tngtech.archunit:archunit-junit5-engine:${Versions.archunit}") {
		because 'contains the ArchUnit TestEngine implementation'
	}
	testRuntimeOnly("org.slf4j:slf4j-jdk14:${Versions.slf4j}") {
		because 'provide appropriate SLF4J binding'
	}
}

test {
	// Opt-in via system property: '-Dplatform.tooling.support.tests.enabled=true'
	enabled = Boolean.getBoolean('platform.tooling.support.tests.enabled')

	// The following if-block is necessary since Gradle will otherwise
	// always publish all mavenizedProjects even if this 'test' task
	// is not executed.
	if (enabled) {
		// All maven-aware projects must be installed, i.e. published to the local repository
		rootProject.ext.mavenizedProjects.each { name ->
			dependsOn project(":${name}").publishToMavenLocal
		}
	}

	filter {
		// Include only tests from this module
		includeTestsMatching "platform.tooling.support.*"
	}
}
